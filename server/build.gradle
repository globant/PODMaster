buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.7.RELEASE")
  }
}

apply plugin: 'java'
sourceSets {
  generated {
    java {
      srcDirs = ['src/generated/java']
    }
  }
}
// Add provided dependency scope, and querydsl scope for querydls integration
configurations {
  provided {
    dependencies.all { dep ->
      configurations.default.exclude group: dep.group, module: dep.name
    }
  }
  compile.extendsFrom provided
  
  querydslapt
}
jar {
  baseName = 'agile-podmaster'
  version =  '0.1'
}

apply plugin: 'spring-boot'
apply plugin: 'eclipse'

apply from: 'buildSrc/jacoco-coverage.gradle'

apply plugin: 'pmd'
pmd {
  ruleSetFiles = files("buildSrc/config/pmd/pmd.xml")
  sourceSets = sourceSets.findAll{ !it.equals(sourceSets.generated) }
}

//http://www.gradle.org/docs/current/userguide/checkstyle_plugin.html
apply plugin: 'checkstyle' 
checkstyle {
  toolVersion = '5.9'
  configFile = file('buildSrc/config/checkstyle/checkstyle-5.7.xml')
  sourceSets = sourceSets.findAll{ !it.equals(sourceSets.generated) }
}

apply plugin: 'findbugs'
findbugs {
  toolVersion = '3.0.0'
  //includeFilter = file("build-config/findbugs/includeFilter.xml")
  sourceSets = sourceSets.findAll{ !it.equals(sourceSets.generated) }
}
tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
 }

repositories {
  mavenCentral()
}

dependencies {
  provided("org.projectlombok:lombok:1.14.8")

  compile("org.springframework.boot:spring-boot-starter-data-jpa")
  compile("org.springframework.hateoas:spring-hateoas")
  compile("org.springframework.boot:spring-boot-starter-data-rest")
  compile("org.springframework.data:spring-data-rest-webmvc:2.2.0.RELEASE") //override default version
  
  compile("commons-codec:commons-codec:1.9")
  compile("org.apache.httpcomponents:httpclient")
    
  // tag::jetty[]
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }
  compile("org.springframework.boot:spring-boot-starter-jetty")
  // end::jetty[]

  // tag::actuator[]
  compile("org.springframework.boot:spring-boot-starter-actuator")
  // end::actuator[]

  runtime("org.liquibase:liquibase-core")

  testCompile("junit:junit")
  testCompile("org.springframework.boot:spring-boot-starter-test")

  runtime("org.hsqldb:hsqldb")
  
  
  
  compile("com.mysema.querydsl:querydsl-jpa:3.5.0")
  querydslapt "com.mysema.querydsl:querydsl-apt:3.5.0"  

  testCompile("com.mysema.querydsl:querydsl-collections:3.5.0")
}

task coverageCheck(type: JacocoCoverageCheck) {
  lineThreshold = 0
  branchThreshold = 0
  instructionThreshold = 0
  //lineThreshold = 0.78
  //branchThreshold = 0.11
  //instructionThreshold = 0.39
  
  reportTask = jacocoTestAllReport
} 
check.dependsOn coverageCheck

distZip.dependsOn check
distTar.dependsOn check

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.querydslapt
  options.compilerArgs = [
    "-proc:only",
    "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
  ]
  destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}
 
compileJava {
  dependsOn generateQueryDSL
  source generateQueryDSL.destinationDir
}
 
compileGeneratedJava {
  dependsOn generateQueryDSL
  options.warnings = false
  classpath += sourceSets.main.runtimeClasspath
}
 
task cleanGenerated << {
  delete sourceSets.generated.java.srcDirs
}
